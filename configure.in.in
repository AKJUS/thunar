dnl $Id$
dnl
dnl Copyright (c) 2004-2005
dnl         The Thunar development team. All rights reserved.
dnl
dnl Written for Thunar by Benedikt Meurer <benny@xfce.org>.
dnl

dnl Version information
m4_define([thunar_version_major], [0])
m4_define([thunar_version_minor], [0])
m4_define([thunar_version_micro], [2])
m4_define([thunar_version_build], [@REVISION@])
m4_define([thunar_version_tag], [svn])
m4_define([thunar_version_date], [@DATE@])
m4_define([thunar_version], [thunar_version_major().thunar_version_minor().thunar_version_micro()ifelse(thunar_version_build(), [], [], [.thunar_version_build()])thunar_version_tag()-thunar_version_date()])

dnl Initialize autoconf
AC_COPYRIGHT([Copyright (c) 2004-2005
        The Thunar development team. All rights reserved.
        
Written for Thunar by Benedikt Meurer <benny@xfce.org>.])
AC_INIT([Thunar], [thunar_version()], [benny@xfce.org])
AC_PREREQ([2.50])
AC_CANONICAL_TARGET()
AC_REVISION([$Id$])

dnl Initialize automake
AM_INIT_AUTOMAKE([AC_PACKAGE_TARNAME()], [AC_PACKAGE_VERSION()])
AM_CONFIG_HEADER([config.h])
AM_MAINTAINER_MODE()

dnl check for UNIX variants
AC_AIX()
AC_ISC_POSIX()
AC_MINIX()

dnl check for basic programs
AC_PROG_CC()
AC_PROG_INSTALL()
AC_PROG_LIBTOOL()

dnl Check for standard headers
AC_CHECK_HEADERS([errno.h fcntl.h fstab.h memory.h stdlib.h string.h \
                  sys/cdio.h sys/event.h \
                  sys/mount.h sys/stat.h sys/time.h sys/param.h time.h])

dnl Check for standard functions
AC_CHECK_FUNCS([kqueue])

dnl Check for required packages
XDT_CHECK_PACKAGE([EXO], [exo-0.3], [0.3.1])
XDT_CHECK_PACKAGE([GTHREAD], [gthread-2.0], [2.6.0])

dnl Check for the system flavour
AC_MSG_CHECKING([for system flavour])
case "$target_os" in
*bsd*)
	FLAVOUR="bsd"
	;;
*)
	FLAVOUR="sysv"
	;;
esac
AC_MSG_RESULT([$FLAVOUR])

dnl Create links for the volume manager implementation
AC_CONFIG_LINKS(
[
  thunar-vfs/thunar-vfs-volume-impl.c:thunar-vfs/thunar-vfs-volume-$FLAVOUR.c
  thunar-vfs/thunar-vfs-volume-impl.h:thunar-vfs/thunar-vfs-volume-$FLAVOUR.h
])

dnl Check for debugging support
BM_DEBUG_SUPPORT()

AC_OUTPUT([
Makefile
docs/Makefile
docs/design/Makefile
tests/Makefile
tests/data/Makefile
thunar/Makefile
thunar-vfs/Makefile
])
